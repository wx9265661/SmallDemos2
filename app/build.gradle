apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.zhanghaochen.smalldemos"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    dataBinding{
        enabled true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.cardview:cardview:1.0.0'
    // 一些三方控件
    implementation "org.jetbrains.anko:anko:0.10.8"
    // jetpack Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$navigationVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navigationVersion"
    // liveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    // viewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
}
apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
}
def coverageSourceDirs = ['../app/src/main/java']
task jacocoTestReport(type: JacocoReport) {
    doFirst {
        delete('../report/testUnitTest.exec')
        delete('../report/androidTestUnitTest.ec')
        delete('../report/test-results/')
    }
    group = "Reporting"
    jacoco {
        reportsDir = file("../report/test-results/")
    }
    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(dir: '../app/build/intermediates/classes/debug',
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*'])

    def utilTest = '../app/build/jacoco/testDebugUnitTest.exec'
    def androidTestDir = '../app/build/outputs/code-coverage/connected/'
    def androidTest
    def androidTestFileName
    FileTree iotTree = fileTree(dir: androidTestDir, includes: ['*.ec'])
    iotTree.each {
        File file ->
            androidTestFileName = file.name
            androidTest = file.path
    }
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files(utilTest, androidTest)

    reports {
        xml.enabled = true
        html.enabled = true
    }
    afterEvaluate {
        jacocoTestReport {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: "../app/build/intermediates/classes/debug/com/zhanghaochen/smalldemos",
                        exclude: [
                                '**/*$*.*',
                        ])
            })
        }
    }
}
task compressJacocoReport(type: Zip) {
    into('../jacocoReport') {
        from "../report/test-results/jacocoTestReport"
        baseName = 'jacocoReport'
    }

    into('../androidTestReport') {
        from "../app/build/reports/androidTests/connected"
        baseName = 'androidTestReport'
    }

    into('../unitTestReport') {
        from "../app/build/reports/tests/testDebugUnitTest"
        baseName = 'unitTestReport'
    }

    destinationDir file("../report/test-results")
}
repositories {
    mavenCentral()
}
